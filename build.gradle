plugins {
    id("java")
    id("io.freefair.lombok") version("8.1.0")
    id("com.github.johnrengelman.shadow") version("7.1.2")
}

group = 'com.jeffmedia'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = "spigotmc-repo"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "aikar"
        url = "https://repo.aikar.co/content/groups/aikar/"
    }
}

dependencies {
    compileOnly("org.spigotmc:spigot-api:1.19.4-R0.1-SNAPSHOT")
    implementation("co.aikar:acf-paper:0.5.1-SNAPSHOT")
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

compileJava {
    options.compilerArgs += ["-parameters"]
    options.fork = true
    options.encoding = "UTF-8"
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset("UTF-8")
    filesMatching("plugin.yml") {
        expand props
    }
}

shadowJar {
    relocate 'co.aikar.commands', 'com.jeffmedia.displayeditor.libs.acf'
    relocate 'co.aikar.locales', 'com.jeffmedia.displayeditor.libs.locales'
}

build.dependsOn(shadowJar)